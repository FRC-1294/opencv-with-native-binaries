plugins {
  id 'java'
  id 'maven-publish'
  id 'org.ajoberstar.grgit' version '2.1.0'
  id 'org.ajoberstar.reckon' version '0.3.0'
  id "com.jfrog.bintray" version "1.7.3"
}

group 'org.usfirst.frc.team1294'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile files("lib/opencv-331.jar")
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

reckon {
  normal = scopeFromProp()
  preRelease = stageFromProp('dev', 'milestone', 'rc', 'final')
}

task unzipOpenCV(type: Copy) {
  def zipFile = file('lib/opencv-331.jar')
  def outputDir = file("${buildDir}/classes/java/main")

  from zipTree(zipFile)
  into outputDir
  include 'org/**'
}
jar.dependsOn unzipOpenCV

publishing {
  publications {
    OpenCV(MavenPublication) {
      from components.java
      artifactId 'opencv-331'
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publications = ['OpenCV']

  publish = true

  pkg {
    repo = 'Robot-Code'
    name = 'opencv-331'
    userOrg = 'frc-1294'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/FRC-1294/opencv-with-native-binaries.git'
  }
}